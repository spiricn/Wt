#version 330

in vec4 inVertex;
in vec2 inTexCoord;
in vec3 inNormals;
in vec4 inBoneIds;
in vec4 inWeights;
in vec3 inTangent;

out vec3 fsWorldPos;
out vec3 fsNormal;
out vec2 fsTexCoord;

const int MAX_BONES = 200;

uniform mat4x4 uModelMat;
uniform mat4x4 uProjMat;
uniform mat4x4 uViewMat;
uniform mat4x4 uBones[MAX_BONES];

uniform int uPassType;

//#define SKIN_DISABLE

void main(void) {
	mat4x4 modelViewMat = uViewMat * uModelMat;
	mat4x4 modelViewProjMat = uProjMat * modelViewMat;

	// vertex normal
	fsNormal = (uModelMat * vec4(inNormals, 0.0)).xyz;

	// vertex position
	vec4 pos = inVertex;

	// is the vertex bound to some bones?
	if(int(inBoneIds[0]) != MAX_BONES){

		mat4x4 boneTransform = mat4(0.0f);

		// generate bone transformation matrix
		for(int i=0; i<4; i++){
			int boneIndex = int( inBoneIds[i] );

			if(boneIndex == MAX_BONES){
				// no more bones
				break;
			}
			else{
				boneTransform += ( uBones[ boneIndex ] * inWeights[i] );
			}
		}

		// animate vertex
		pos = boneTransform*pos;

		fsNormal = (boneTransform*vec4(fsNormal, 0.0)).xyz;
	}

	fsTexCoord = inTexCoord.st;
	
	// TODO might be wrong
	fsWorldPos = (uModelMat * pos).xyz;

	gl_Position = modelViewProjMat * pos;
}