#version 330

in vec4 inVertex;
in vec2 inTexCoord;
in vec3 inNormals;
in vec4 inBoneIds;
in vec4 inWeights;
in vec3 inTangent;

out vec3 fsWorldPos;
out vec3 fsNormal;
out vec2 fsTexCoord;
out vec3 fsTangent;

const int MAX_BONES = 200;

uniform mat4x4 uModelMat;
uniform mat4x4 uProjMat;
uniform mat4x4 uViewMat;
uniform mat4x4 uBones[MAX_BONES];

uniform int uPassType;

//#define SKIN_DISABLE

void main(){
	// Transform matrices
	mat4x4 modelViewMat = uViewMat * uModelMat;
	mat4x4 modelViewProjMat = uProjMat * modelViewMat;

	// Vertex position
	vec4 pos = inVertex;

	// Vertex normal
	vec4 normal = vec4(inNormals, 0.0);

	// Ss the vertex bound to some bones?
	if(int(inBoneIds[0]) != MAX_BONES){

		mat4x4 boneTransform = mat4(0.0f);

		// generate bone transformation matrix
		for(int i=0; i<4; i++){
			int boneIndex = int( inBoneIds[i] );

			if(boneIndex == MAX_BONES){
				// no more bones
				break;
			}
			else{
				boneTransform += ( uBones[ boneIndex ] * inWeights[i] );
			}
		}

		// Animate position
		pos = boneTransform*pos;

		// Animate normal
		normal = boneTransform*normal;
	}

	fsTexCoord = inTexCoord.st;
	
	fsWorldPos = (uModelMat * pos).xyz;

	fsNormal = (uModelMat * normal).xyz;

	// Pass through tangent
	fsTangent = inTangent;

	gl_Position = modelViewProjMat * pos;
}